apply plugin: 'com.android.application'

//当前beta版本号
ext.build_number = "beta1";
//显示的时间格式，形如20170710
static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getDefault());
}
android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.dotawang.mvpdemo3"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        ndk {
            abiFilters 'armeabi', 'x86'
        }
        //语言类型
        resConfigs "en", "fr"
    }
    //引入jar和aar三方包的地址
    repositories {
        flatDir {
            dirs 'libs', 'modules'
        }
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['jniLibs']
        }
    }

    signingConfigs {
        release {
            storeFile file('/release.jks')
            storePassword '123456'
            keyAlias 'dotawang'
            keyPassword '123456'
        }
        debug {
            storeFile file('/release.jks')
            storePassword '123456'
            keyAlias 'dotawang'
            keyPassword '123456'
        }
    }

    buildTypes {
        release {
            minifyEnabled false //混淆时设置为true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
    }

    configurations.all {
        resolutionStrategy.force 'com.android.support:support-annotations:26.1.0'
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    // 打包命名
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                // 显示app名称+当前版本号
                def fileName = "汪洋-V${variant.versionName}"
                // 如果有渠道，显示渠道名""
                if (variant.flavorName != "") {
                    fileName = fileName + "-${variant.flavorName}"
                }
                // 加入打包时间
                fileName = fileName + "-${releaseTime()}"
                // 如果是debug版本，加入beta版本号
                if ('debug'.equals(variant.buildType.name)) {
                    fileName = fileName + "-${build_number}.apk"
                } else {
                    // 如果是release版本，直接显示
                    fileName = fileName + "-release.apk"
                }
                outputFileName = fileName
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    //noinspection GradleCompatible
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:support-annotations:28.0.0'
    //butterknife
    implementation 'com.jakewharton:butterknife:9.0.0-rc1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:9.0.0-rc1'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
    //rx 6.0权限请求
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.5@aar'
    //network
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    //rx java
    implementation 'io.reactivex.rxjava2:rxjava:2.2.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    //rx管理View的生命周期
    implementation 'com.trello.rxlifecycle2:rxlifecycle:2.2.1'
    implementation 'com.trello.rxlifecycle2:rxlifecycle-components:2.2.1'
    //json解析
    implementation 'com.google.code.gson:gson:2.8.5'
    //eventbus
    implementation 'org.greenrobot:eventbus:3.0.0'
    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    //AndroidUtilCode工具类集合
    implementation 'com.blankj:utilcode:1.21.2'
    //GreenDao
    implementation 'org.greenrobot:greendao:3.2.2'
    //recyclerview
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    //dialog-material
//    implementation 'com.afollestad.material-dialogs:core:0.9.4.5'
//    implementation 'com.afollestad.material-dialogs:commons:0.9.4.5'
}
